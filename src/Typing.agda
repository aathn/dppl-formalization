module Typing (â„ : Set) where

open import Syntax â„

open import Lib.Prelude
open import Lib.Unfinite
open import Lib.oc-Sets
open import Lib.AbstractionConcretion hiding (abs)
open import Lib.BindingSignature

open import Function using (_âˆ˜_ ; _$_ ; const)
open import Data.List using (map)
open import Data.List.Relation.Binary.Sublist.Propositional using (_âŠ†_)
open import Data.Nat using (_âŠ”_)
open import Relation.Binary using (Rel)

TyEnv : Set
TyEnv = List (ğ”¸ Ã— Type)

pattern [_âˆ¶_]   x T = (x , T) :: []
pattern _,_âˆ¶_ Î“ x T = (x , T) :: Î“

dom : TyEnv â†’ Fset ğ”¸
dom [] = Ã˜
dom (Î“ , x âˆ¶ _) = [ x ] âˆª dom Î“

DistinctName : Rel (ğ”¸ Ã— Type) _
DistinctName (x , _) (xâ‚ , _) = Â¬ x â‰¡ xâ‚

open import Data.List.Relation.Unary.AllPairs.Core DistinctName
  using () renaming (AllPairs to Distinct)

_â‡’áµ–_ : âˆ€ {n} â†’ Vector Coeff n â†’ Type â†’ Vector Coeff n Ã— Type
_â‡’áµ–_ = _,_

PrimTy : (Ï• : Prim) â†’ Vector Coeff (PrimAr Ï•) Ã— Type
PrimTy padd        = const ca â‡’áµ– treal ca
PrimTy pmul        = const ca â‡’áµ– treal ca
PrimTy (pwiener r) = const cc â‡’áµ– treal cc

DistTy : (D : Dist) â†’ Vector Coeff (DistAr D) Ã— Type
DistTy dnormal = const cc â‡’áµ– treal cc
DistTy dbeta   = const cc â‡’áµ– treal cc
DistTy dwiener = (Î»()) â‡’áµ– (treal cc â‡’[ det ] treal cc)

_âŠ™_ : Coeff â†’ Type â†’ Type
c âŠ™ (treal câ€²) = treal (c âŠ” câ€²)
c âŠ™ (ttup Ts)  = ttup $ c âŠ™_ âˆ˜ Ts
c âŠ™ T          = T

_âŠ™á´±_ : Coeff â†’ TyEnv â†’ TyEnv
c âŠ™á´± Î“ = map (Î» (x , T) â†’ x , c âŠ™ T) Î“

infix 5 _<:_
data _<:_ : Type â†’ Type â†’ Set where

  sreal
    : âˆ€ {c câ€²}
    â†’ câ€² â‰¤ c
    â†’ -------------------
      treal c <: treal câ€²

  stup
    : âˆ€ {n Ts Tsâ€²}
    â†’ (âˆ€ (i : Fin n) â†’ Ts i <: Tsâ€² i)
    â†’ -------------------------------
      ttup Ts <: ttup Tsâ€²

  sarr
    : âˆ€ {Tâ‚ Tâ‚â€² Tâ‚‚ Tâ‚‚â€² e eâ€²}
    â†’ Tâ‚â€² <: Tâ‚ â†’ Tâ‚‚ <: Tâ‚‚â€² â†’ e â‰¤ eâ€²
    â†’ -------------------------------
      Tâ‚ â‡’[ e ] Tâ‚‚ <: Tâ‚â€² â‡’[ eâ€² ] Tâ‚‚â€²

  sdist
    : âˆ€ {T Tâ€²}
    â†’ T <: Tâ€²
    â†’ -------------------
      tdist T <: tdist Tâ€²


infix 4 _âŠ¢_:[_]_
data _âŠ¢_:[_]_ : TyEnv â†’ Term â†’ Eff â†’ Type â†’ Set where

  tvar
    : âˆ€ {x T}
    â†’ -----------------------------
      [ x âˆ¶ T ] âŠ¢ fvar x :[ det ] T

  tabs
    : âˆ€ {Î“ Tâ‚ Tâ‚‚ t e}
    â†’ Ğ˜ x âˆ¶ ğ”¸ , Î“ , x âˆ¶ Tâ‚ âŠ¢ conc (t â‚€) x :[ e ] Tâ‚‚
    â†’ ---------------------------------------------
      Î“ âŠ¢ abs Tâ‚ t :[ det ] Tâ‚ â‡’[ e ] Tâ‚‚

  tapp
    : âˆ€ {Î“ ts e Tâ‚ Tâ‚‚}
    â†’ Î“ âŠ¢ ts â‚€ :[ e ] Tâ‚ â‡’[ e ] Tâ‚‚
    â†’ Î“ âŠ¢ ts â‚ :[ e ] Tâ‚
    â†’ --------------------
      Î“ âŠ¢ app ts :[ e ] Tâ‚‚

  tprim
    : âˆ€ {Ï• Î“ cs T ts e}
    â†’ PrimTy Ï• â‰¡ cs â‡’áµ– T
    â†’ (âˆ€ i â†’ Î“ âŠ¢ ts i :[ e ] treal (cs i))
    â†’ ------------------------------------
      Î“ âŠ¢ prim Ï• ts :[ e ] T

  treal

    : âˆ€ {r}
    â†’ -----------------------------
      [] âŠ¢ real r :[ det ] treal cc

  ttup
    : âˆ€ {n Î“ Ts ts e}
    â†’ (âˆ€ i â†’ Î“ âŠ¢ ts i :[ e ] Ts i)
    â†’ -----------------------------
      Î“ âŠ¢ tup {n} ts :[ e ] ttup Ts

  tproj
    : âˆ€ {n Ts Î“ t e} i
    â†’ Î“ âŠ¢ t â‚€ :[ e ] ttup Ts
    â†’ ----------------------------
      Î“ âŠ¢ proj {n} i t :[ e ] Ts i

  tif
    : âˆ€ {Î“ ts e T}
    â†’ Î“ âŠ¢ ts â‚€ :[ e ] treal cb
    â†’ Î“ âŠ¢ ts â‚ :[ e ] T
    â†’ Î“ âŠ¢ ts â‚‚ :[ e ] T
    â†’ ------------------
      Î“ âŠ¢ if ts :[ e ] T

  tdiff
    : âˆ€ {Î“ ts n m cs ds e}
    â†’ (âˆ€ i â†’ cs i â‰¤ cb)
    â†’ Î“ âŠ¢ ts â‚€ :[ e ] treals {n} cs â‡’[ det ] treals {m} ds
    â†’ Î“ âŠ¢ ts â‚ :[ e ] treals cs
    â†’ -----------------------------------------------------------
      Î“ âŠ¢ diff ts :[ e ] treals {n} (const ca) â‡’[ det ] treals ds

  tsolve
    : âˆ€ {Î“ ts n c cs e}
    â†’ Î“ âŠ¢ ts â‚€ :[ e ] ttup {2} (Î» {â‚€ â†’ treal c; â‚ â†’ treals {n} cs}) â‡’[ det ] treals cs
    â†’ Î“ âŠ¢ ts â‚ :[ e ] treals cs
    â†’ Î“ âŠ¢ ts â‚‚ :[ e ] treal c
    â†’ -----------------------------
      Î“ âŠ¢ solve ts :[ e ] treals cs

  tdist
    : âˆ€ {D Î“ cs T ts e}
    â†’ DistTy D â‰¡ cs â‡’áµ– T
    â†’ (âˆ€ i â†’ Î“ âŠ¢ ts i :[ e ] treal (cs i))
    â†’ ------------------------------------
      Î“ âŠ¢ dist D ts :[ e ] tdist T

  tassume
    : âˆ€ {Î“ t T}
    â†’ Î“ âŠ¢ t â‚€ :[ rnd ] tdist T
    â†’ -----------------------
      Î“ âŠ¢ assume t :[ rnd ] T

  tweight
    : âˆ€ {Î“ t}
    â†’ Î“ âŠ¢ t â‚€ :[ rnd ] treal cc
    â†’ ---------------------------
      Î“ âŠ¢ weight t :[ rnd ] tunit

  texpect
    : âˆ€ {Î“ t e}
    â†’ Î“ âŠ¢ t â‚€ :[ e ] tdist (treal cc)
    â†’ -----------------------------
      Î“ âŠ¢ expect t :[ e ] treal cc

  tinfer
    : âˆ€ {Î“ t e T}
    â†’ Î“ âŠ¢ t â‚€ :[ e ] tunit â‡’[ rnd ] T
    â†’ -------------------------------
      Î“ âŠ¢ infer t :[ e ] tdist T

  tweaken
    : âˆ€ {Î“ Î“â€² t e T}
    â†’ Î“ âŠ¢ t :[ e ] T
    â†’ Î“ âŠ† Î“â€²
    â†’ Distinct Î“â€²
    â†’ ---------------
      Î“â€² âŠ¢ t :[ e ] T

  tsub
    : âˆ€ {Î“ t e eâ€² T Tâ€²}
    â†’ Î“ âŠ¢ t :[ e ] T
    â†’ e â‰¤ eâ€²
    â†’ T <: Tâ€²
    â†’ ----------------
      Î“ âŠ¢ t :[ eâ€² ] Tâ€²

  tpromote
    : âˆ€ {Î“ Î“â€² t e c T}
    â†’ Î“ âŠ¢ t :[ e ] T
    â†’ Î“â€² â‰¡ c âŠ™á´± Î“
    â†’ -------------------
      Î“â€² âŠ¢ t :[ e ] c âŠ™ T
