open import Lib.Reals

module Typing (R : Realsâ‚€) where

open Reals R
open import Syntax R

open import Lib.Prelude
open import Lib.LocallyNameless.Unfinite
open import Lib.LocallyNameless.AbstractionConcretion hiding (abs)
open import Lib.LocallyNameless.BindingSignature
open import Lib.Env

open import Data.List.Relation.Binary.Sublist.Propositional using (_âŠ†_)
open import Data.List.Relation.Binary.Pointwise using (Pointwise)
open import Data.List.Relation.Unary.All using (All)

TyEnv : Set
TyEnv = Env Type

variable
  Î“ Î“â€² : TyEnv

PrimTy : (Ï• : Prim) â†’ Vector Coeff (PrimAr Ï•) Ã— Coeff
PrimTy padd        = const A , A
PrimTy pmul        = const A , A
PrimTy psin        = const A , A
PrimTy (pwiener r) = const N , N

DistTy : (D : Dist) â†’ Vector Coeff (DistAr D) Ã— Type
DistTy dnormal = const N , treal N
DistTy dbeta   = const N , treal N
DistTy dwiener = (Î»()) , (treal N â‡’[ det ] treal N)

_âŠ™_ : Coeff â†’ Type â†’ Type
c âŠ™ treal câ€² = treal (c âŠ”â€² câ€²)
c âŠ™ ttup n Ts = ttup n $ c âŠ™_ âˆ˜ Ts
c âŠ™ (Tâ‚ â‡’[ e ] Tâ‚‚) = (c âŠ™ Tâ‚) â‡’[ e ] (c âŠ™ Tâ‚‚)
c âŠ™ tdist T = tdist T

_â‰¤á¶œ_ : Coeff â†’ Type â†’ Set
c â‰¤á¶œ treal d = c â‰¤â€² d
c â‰¤á¶œ ttup n Ts = âˆ€ i â†’ c â‰¤á¶œ Ts i
c â‰¤á¶œ (Tâ‚ â‡’[ _ ] Tâ‚‚) = c â‰¤á¶œ Tâ‚ Ã— c â‰¤á¶œ Tâ‚‚
c â‰¤á¶œ tdist T = ğŸ™

_â‰¤á´±_ : Coeff â†’ TyEnv â†’ Set
c â‰¤á´± Î“ = All (c â‰¤á¶œ_ âˆ˜ Ï€â‚‚) Î“

infix 5 _<:_
data _<:_ : Type â†’ Type â†’ Set where

  sreal :
    (_ : câ€² â‰¤â€² c)
    â†’ -----------------
    treal c <: treal câ€²

  stup :
    {Ts Tsâ€² : Vector Type n}
    (_ : âˆ€ i â†’ Ts i <: Tsâ€² i)
    â†’ -----------------------
    ttup n Ts <: ttup n Tsâ€²

  sarr :
    {Tâ‚ Tâ‚â€² Tâ‚‚ Tâ‚‚â€² : Type}
    (_ : Tâ‚â€² <: Tâ‚)
    (_ : Tâ‚‚ <: Tâ‚‚â€²)
    (_ : e â‰¤â€² eâ€²)
    â†’ -----------------------------
    Tâ‚ â‡’[ e ] Tâ‚‚ <: Tâ‚â€² â‡’[ eâ€² ] Tâ‚‚â€²

  sdist :
    (_ : T <: Tâ€²)
    â†’ -----------------
    tdist T <: tdist Tâ€²


_<:á´±_ : TyEnv â†’ TyEnv â†’ Set
_<:á´±_ = Pointwise (Î» (xâ‚ , Tâ‚) (xâ‚‚ , Tâ‚‚) â†’ xâ‚ â‰¡ xâ‚‚ Ã— Tâ‚ <: Tâ‚‚)


infix 4 _âŠ¢_:[_,_]_
data _âŠ¢_:[_,_]_ : TyEnv â†’ Term â†’ Coeff â†’ Eff â†’ Type â†’ Set where

  tvar :
    {x : ğ”¸}
    (_ : [ x âˆ¶ T ] âŠ† Î“)
    (_ : c â‰¤á¶œ T)
    (_ : Distinct Î“)
    â†’ -----------------------
    Î“ âŠ¢ fvar x :[ c , det ] T

  tabs :
    {Tâ‚ Tâ‚‚ : Type}
    {t : Vector Term 1}
    (_ : Ğ˜ x âˆ¶ ğ”¸ , Î“ , x âˆ¶ c âŠ™ Tâ‚ âŠ¢ conc (t â‚€) x :[ c , e ] Tâ‚‚)
    â†’ ---------------------------------------------------------
    Î“ âŠ¢ abs Tâ‚ â–¸ t :[ c , det ] Tâ‚ â‡’[ e ] Tâ‚‚

  tapp :
    {Tâ‚ Tâ‚‚ : Type}
    {ts : Vector Term 2}
    (_ : Î“ âŠ¢ ts â‚€ :[ c , e ] Tâ‚ â‡’[ e ] Tâ‚‚)
    (_ : Î“ âŠ¢ ts â‚ :[ c , e ] Tâ‚)
    â†’ ------------------------------------
    Î“ âŠ¢ app â–¸ ts :[ c , e ] Tâ‚‚

  tprim :
    {cs : Vector Coeff (PrimAr Ï•)}
    {ts : Vector Term (PrimAr Ï•)}
    (_ : PrimTy Ï• â‰¡ (cs , câ€²))
    (_ : Distinct Î“)
    (_ : âˆ€ i â†’ Î“ âŠ¢ ts i :[ c , e ] treal (cs i))
    â†’ ------------------------------------------
    Î“ âŠ¢ prim Ï• â–¸ ts :[ c , e ] treal câ€²

  treal :
    (Hd : Distinct Î“)
    â†’ -----------------------------
    Î“ âŠ¢ real r :[ c , det ] treal A

  ttup :
    {Ts : Vector Type n}
    {ts : Vector Term n}
    (_ : Distinct Î“)
    (_ : âˆ€ i â†’ Î“ âŠ¢ ts i :[ c , e ] Ts i)
    â†’ ----------------------------------
    Î“ âŠ¢ tup n â–¸ ts :[ c , e ] ttup n Ts

  tproj :
    {Ts : Vector Type n}
    {t : Vector Term 1}
    (i : Fin n)
    (_ : Î“ âŠ¢ t â‚€ :[ c , e ] ttup n Ts)
    â†’ --------------------------------
    Î“ âŠ¢ proj n i â–¸ t :[ c , e ] Ts i

  tif :
    {ts : Vector Term 3}
    (_ : Î“ âŠ¢ ts â‚€ :[ P , e ] treal A)
    (_ : Î“ âŠ¢ ts â‚ :[ c , e ] T)
    (_ : Î“ âŠ¢ ts â‚‚ :[ c , e ] T)
    â†’ -------------------------------
    Î“ âŠ¢ if â–¸ ts :[ c , e ] T

  tdiff :
    {ts : Vector Term 2}
    {cs : Vector Coeff n}
    {csâ€² : Vector Coeff m}
    (_ : âˆ€ i â†’ cs i â‰¤â€² P)
    (_ : c â‰¤â€² P)
    (_ : Î“ âŠ¢ ts â‚€ :[ c , e ] treals n cs â‡’[ det ] treals m csâ€²)
    (_ : Î“ âŠ¢ ts â‚ :[ c , e ] treals n cs)
    â†’ ---------------------------------------------------------------
    Î“ âŠ¢ diff â–¸ ts :[ c , e ] treals n (const A) â‡’[ det ] treals m csâ€²

  -- TODO: Fix the typing rule for solve 
  tsolve :
    {Î“ : TyEnv}
    {ts : Vector Term 3}
    {cs : Vector Coeff n}
    {câ‚€ : Coeff}
    (_ : Î“ âŠ¢ ts â‚€ :[ câ‚€ , e ] (ttup 2 Î» {â‚€ â†’ treal câ€²; â‚ â†’ treals n cs}) â‡’[ det ] treals n cs)
    (_ : Î“ âŠ¢ ts â‚ :[ câ‚€ , e ] ttup 2 Î» {â‚€ â†’ treal c; â‚ â†’ treals n cs})
    (_ : Î“ âŠ¢ ts â‚‚ :[ câ‚€ , e ] treal câ€²)
    â†’ ------------------------------------------------------------------------------------------------------
    Î“ âŠ¢ solve â–¸ ts :[ câ‚€ , e ] ttup 2 Î» {â‚€ â†’ treal c; â‚ â†’ treals n cs}

  tdist :
    {cs : Vector Coeff (DistAr D)}
    {ts : Vector Term (DistAr D)}
    (_ : DistTy D â‰¡ (cs , T))
    (_ : Distinct Î“)
    (_ : âˆ€ i â†’ Î“ âŠ¢ ts i :[ c , e ] treal (cs i))
    â†’ ------------------------------------------
    Î“ âŠ¢ dist D â–¸ ts :[ c , e ] tdist T

  tassume :
    {t : Vector Term 1}
    (_ : Î“ âŠ¢ t â‚€ :[ c , rnd ] tdist T)
    â†’ --------------------------------
    Î“ âŠ¢ assume â–¸ t :[ c , rnd ] T

  tweight :
    {t : Vector Term 1}
    (_ : Î“ âŠ¢ t â‚€ :[ N , rnd ] treal A)
    â†’ --------------------------------
    Î“ âŠ¢ weight â–¸ t :[ c , rnd ] tunit

  tinfer :
    {t : Vector Term 1}
    (_ : Î“ âŠ¢ t â‚€ :[ N , e ] tunit â‡’[ rnd ] T)
    â†’ ---------------------------------------
    Î“ âŠ¢ infer â–¸ t :[ c , e ] tdist T

  tsub :
    (_ : Î“ âŠ¢ t :[ c , e ] T)
    (_ : e â‰¤â€² eâ€²)
    (_ : T <: Tâ€²)
    â†’ ----------------------
    Î“ âŠ¢ t :[ c , eâ€² ] Tâ€²

  tpromote :
    (_ : Î“ âŠ¢ t :[ c , e ] T)
    (_ : câ€² â‰¤â€² c)
    â†’ ----------------------
    Î“ âŠ¢ t :[ câ€² ,  e ] c âŠ™ T

  tdemote :
    (_ : Î“ âŠ¢ t :[ câ€² , e ] c âŠ™ T)
    (_ : câ€² â‰¤â€² c)
    â†’ ---------------------------
    Î“ âŠ¢ t :[ c , e ] T
