module Lib.Concrete.Concrete where

-- Our definitions of concrete categories, sites, and sheaves.

open import Lib.Prelude hiding (id ; _âˆ˜_ ; _âˆˆ_ ; [_])

open import Categories.Category using (Category)
open import Categories.Category.Construction.Presheaves using (Presheaves)
open import Categories.Category.Instance.Setoids using (Setoids)
open import Categories.Category.SubCategory using (FullSubCategory)
open import Categories.Functor using (Functor)
open import Categories.Functor.Hom using (module Hom)
open import Categories.Functor.Presheaf using (Presheaf)
open import Categories.Functor.Properties using (Faithful)
open import Categories.Object.Terminal using (Terminal)

open import Function using (Func)
import Function.Construct.Setoid as FnS

open import Relation.Unary using (_âˆˆ_ ; _âŠ†_)
open import Relation.Binary.Bundles using (Setoid)

open Func

module _ {aâ‚ aâ‚‚ bâ‚ bâ‚‚ : Level} {A : Setoid aâ‚ aâ‚‚} {B : Setoid bâ‚ bâ‚‚} where
  open Setoid B
  Im : Func A B â†’ â„™ Carrier _
  Im f y = âˆƒ Î» x â†’ y â‰ˆ f .to x

  _â‰—_ : Rel (Func A B) _
  _â‰—_ = F._â‰ˆ_
    where module F = Setoid (FnS.setoid A B)

module _ {c â„“ â„“â€² : Level} (A : Setoid c â„“) where
  open Setoid A
  record âˆƒ! (P : â„™ Carrier â„“â€²) : Set (c âŠ” â„“ âŠ” â„“â€²) where
    constructor mkUnique
    field
      witness  : Carrier
      has-prop : P witness
      unique   : (x : Carrier) â†’ P x â†’ witness â‰ˆ x

  open âˆƒ! public

record CCat (o â„“ e : Level) : Set (lsuc (o âŠ” â„“ âŠ” e)) where
  -- Here, we use a more restrictive definition of concrete category
  -- than the standard presentation in that we require the forgetful
  -- functor to be representable by a terminal object
  -- (following Matache et al.).
  field
    Cat : Category o â„“ e

  open Category Cat public
  open Hom Cat public

  field
    terminal : Terminal Cat

  open Terminal terminal public renaming (âŠ¤ to â‹† ; âŠ¤-is-terminal to â‹†-is-terminal)

  field
    â‹†-hom-faithful : Faithful Hom[ â‹† ,-]

  objâˆ£_âˆ£ : Obj â†’ Setoid â„“ e
  objâˆ£ c âˆ£ = Hom[ â‹† , c ]

  homâˆ£_âˆ£ : {oâ‚ oâ‚‚ : Obj} â†’ oâ‚ â‡’ oâ‚‚ â†’ Func objâˆ£ oâ‚ âˆ£ objâˆ£ oâ‚‚ âˆ£
  homâˆ£ f âˆ£ = record {
      to = Î» g â†’ f âˆ˜ g
    ; cong = âˆ˜-resp-â‰ˆÊ³
    }

  module obj-set (c : Obj) = Setoid Hom[ â‹† , c ]


module _ {o â„“ e : Level} where

  module Sieve (C : Category o â„“ e) where
    -- In contrast to Matache et al. (2022), we choose to work primarily
    -- with covering *sieves* (and Grothendieck Topologies), which
    -- simplifies definitions.  For coverage-based sites satisfying
    -- axioms (L) and (M) (as presented in Matache et al.), there is
    -- always a corresponding Grothendieck topology which gives rise
    -- to the same sheaves.

    -- We adapt definitions from the 1lab (which we unfortunately cannot
    -- use as-is since we cannot easily switch to Cubical Agda).
    -- https://1lab.dev/Cat.Diagram.Sieve.html

    open Category C

    record Sieve (U : Obj) (i : Level) : Set (o âŠ” â„“ âŠ” e âŠ” lsuc i) where
      field
        arrows : âˆ€ {V} â†’ â„™ (V â‡’ U) i
        closed : âˆ€ {V W f} (Hf : f âˆˆ arrows) (g : V â‡’ W) â†’ f âˆ˜ g âˆˆ arrows
        resp-â‰ˆ : âˆ€ {V} {f g} â†’ f â‰ˆ g â†’ f âˆˆ arrows {V} â†’ g âˆˆ arrows

    open Sieve public

    private
      variable
        i â„“â€² : Level

    infix 4 _âˆˆË¢_ _âŠ†Ë¢_
    _âˆˆË¢_ : âˆ€ {U} {V} â†’ V â‡’ U â†’ Sieve U i â†’ Set i
    f âˆˆË¢ S = f âˆˆ S .arrows

    _âŠ†Ë¢_ : âˆ€ {U} â†’ Rel (Sieve U i) (o âŠ” â„“ âŠ” i)
    S âŠ†Ë¢ R = âˆ€ {V} â†’ S .arrows {V} âŠ† R .arrows

    maximal : âˆ€ {U} â†’ Sieve U i
    maximal .arrows x = ğŸ™
    maximal .closed g x = tt
    maximal .resp-â‰ˆ Hâ‰ˆ Hf = tt

    intersect : âˆ€ {U} {I : Set â„“â€²} (F : I â†’ Sieve U i) â†’ Sieve U (i âŠ” â„“â€²)
    intersect {I = I} F .arrows h = (x : I) â†’ h âˆˆË¢ F x
    intersect {I = I} F .closed Hx g i = F i .closed (Hx i) g
    intersect {I = I} F .resp-â‰ˆ Hâ‰ˆ Hf i = F i .resp-â‰ˆ Hâ‰ˆ (Hf i)

    pullback : âˆ€ {U V} â†’ V â‡’ U â†’ Sieve U i â†’ Sieve V i
    pullback f S .arrows h = f âˆ˜ h âˆˆË¢ S
    pullback f S .closed Hf g = S .resp-â‰ˆ assoc (S .closed Hf g)
    pullback f S .resp-â‰ˆ Hâ‰ˆ Hf = S .resp-â‰ˆ (âˆ˜-resp-â‰ˆÊ³ Hâ‰ˆ) Hf

  module Matching
    {oâ€² eâ€² : Level}
    {C : Category o â„“ e}
    (A : Presheaf C (Setoids oâ€² eâ€²))
    where
    -- We define matching families over sieves, adapting the definitions at
    -- https://1lab.dev/Cat.Site.Base.html

    open Category C
    open Sieve C

    private
      module A = Functor A
      module Aâ‚€ (U : Obj) = Setoid (A.â‚€ U)

      variable
        i : Level

    -- Maybe this could be made into a Func from a setoid of pairs f , Hf
    -- where equality only applies to f...
    record Parts {U : Obj} (S : Sieve U i) : Set (o âŠ” â„“ âŠ” e âŠ” i âŠ” oâ€² âŠ” eâ€²) where
      field
        to : âˆ€ {V} {f : V â‡’ U} â†’ f âˆˆË¢ S â†’ Aâ‚€.Carrier V
        cong :
          âˆ€ {V} {f g : V â‡’ U} {Hf : f âˆˆË¢ S} {Hg : g âˆˆË¢ S}
          â†’ f â‰ˆ g â†’ Aâ‚€._â‰ˆ_ V (to Hf) (to Hg)

    open Parts public

    is-matching : {U : Obj} (S : Sieve U i) â†’ Parts S â†’ Set _
    is-matching {U = U} S p =
      âˆ€ {V W} {f : V â‡’ U} (Hf : f âˆˆË¢ S) {g : W â‡’ V} (Hgf : f âˆ˜ g âˆˆË¢ S)
      â†’ [ W ] A.â‚ g .to (p .to Hf) â‰ˆ p .to Hgf
      where [_]_â‰ˆ_ = Aâ‚€._â‰ˆ_

    is-section : {U : Obj} (S : Sieve U i) â†’ Parts S â†’ Aâ‚€.Carrier U â†’ Set _
    is-section {U = U} S p s =
      âˆ€ {V} {f : V â‡’ U} (Hf : f âˆˆË¢ S) â†’ [ V ] A.â‚ f .to s â‰ˆ p .to Hf
      where [_]_â‰ˆ_ = Aâ‚€._â‰ˆ_


  record CSite (ğ’ : CCat o â„“ e) (i p : Level) : Set (o âŠ” â„“ âŠ” e âŠ” lsuc (i âŠ” p)) where
    -- Grothendieck sites (in a concrete rendering), adapted from
    -- https://1lab.dev/Cat.Site.Grothendieck.html
    open CCat ğ’
    open Sieve Cat public

    field
      covering : (U : Obj) â†’ â„™ (Sieve U i) p

    _â—€_ : (U : Obj) (R : Sieve U i) â†’ Set p
    _â—€_ = covering

    field
      is-stable :
        âˆ€ {U V} (g : V â‡’ U) {R : Sieve U i} (_ : U â—€ R)
        â†’ ---------------------------------------------
        V â—€ pullback g R

      is-concrete :
        âˆ€ {U} {R : Sieve U i} (_ : U â—€ R) (x : obj-set.Carrier U)
        â†’ -------------------------------------------------------
        âˆƒâ‚‚ Î» V (f : V â‡’ U) â†’ f âˆˆË¢ R Ã— x âˆˆ Im homâˆ£ f âˆ£

      is-maximal : âˆ€ {U} {R : Sieve U i} â†’ id âˆˆË¢ R â†’ U â—€ R

      is-local :
        âˆ€ {U} {R S : Sieve U i} (_ : U â—€ R)
        â†’ (âˆ€ {V} {f : V â‡’ U} â†’ f âˆˆË¢ R â†’ V â—€ pullback f S)
        â†’ -----------------------------------------------
        U â—€ S

    upward-closed :
      âˆ€ {U} {R S : Sieve U i} (_ : U â—€ R)
      â†’ ---------------------------------
      R âŠ†Ë¢ S â†’ U â—€ S
    upward-closed {S = S} HR HâŠ† =
      is-local HR Î» Hf â†’ is-maximal (S .closed (HâŠ† Hf) id)


  record CSheaf
    {i p : Level}
    {ğ’ : CCat o â„“ e}
    (S : CSite ğ’ i p)
    (oâ€² eâ€² : Level)
    : -------------------------------------------
    Set (o âŠ” â„“ âŠ” e âŠ” lsuc i âŠ” p âŠ” lsuc (oâ€² âŠ” eâ€²))
    where
    -- Concrete sheaves over concrete sites, with inspiration from
    -- https://1lab.dev/Cat.Site.Base.html
    open CCat ğ’
    open CSite S

    field
      Psh : Presheaf Cat (Setoids oâ€² eâ€²)

    open Functor Psh public
    open Matching Psh public

    module Fâ‚€ (U : Obj) = Setoid (Fâ‚€ U)
    module X = Fâ‚€ â‹†

    âˆ£_âˆ£ : Setoid oâ€² eâ€²
    âˆ£_âˆ£ = Fâ‚€ â‹†

    F-maps : (U : Obj) â†’ Fâ‚€.Carrier U â†’ Func objâˆ£ U âˆ£ âˆ£_âˆ£
    F-maps U FU = record
      { to = Î» f â†’ Fâ‚ f .to FU
      ; cong = Î» Hâ‰ˆ â†’ F-resp-â‰ˆ Hâ‰ˆ
      }

    R[_,_] : (U : Obj) â†’ â„™ (Func objâˆ£ U âˆ£ âˆ£_âˆ£) _
    R[_,_] U f = âˆƒ Î» FU â†’ f â‰— F-maps U FU

    field
      is-sheaf :
        âˆ€ {U} {R : Sieve U i} (_ : U â—€ R)
        (p : Parts R) (_ : is-matching R p)
        â†’ ---------------------------------
        âˆƒ! (Fâ‚€ U) (is-section R p)

      is-concrete :
        âˆ€ {U} â†’ injection (Fâ‚€._â‰ˆ_ U) _â‰—_ (F-maps U)


module _ {o â„“ e i p : Level} {ğ’ : CCat o â„“ e} (S : CSite ğ’ i p) (oâ€² eâ€² : Level) where
  open CCat ğ’

  -- Finally, we define the category of concrete sheaves over a fixed
  -- site.  This is just the subcategory of presheaves whose objects
  -- are concrete sheaves.
  
  CSheaves : Category _ _ _
  CSheaves = FullSubCategory (Presheaves Cat) (CSheaf.Psh {S = S} {oâ€²} {eâ€²})
